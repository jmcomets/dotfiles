" Summary -------------------------------------------------------- {{{
"
" Author: Jean-Marie Comets <jean.marie.comets@gmail.com>
"
" Sections:
"   - Preamble
"   - Vim user interface
"   - Syntax related
"   - Status line
"   - Text, tab and indent related
"   - Colors and Fonts
"   - Moving around, tabs and buffers
"   - Searching
"   - Various key remaps
"   - Helper functions
"   - Filetype settings
"   - Plugin customization
"
" ---------------------------------------------------------------- }}}

" Preamble ------------------------------------------------------- {{{

" Sets how many lines of history Vim has to remember
set history=700

" Set to auto read when a file is changed from the outside
set autoread

" Vim startup message
set shortmess+=I

" Set mapleader (shortcut key) to ","
let mapleader = ","
let g:mapleader = ","

" Set utf8 as standard encoding and en_US as the standard language
set encoding=utf8

" Use Unix as the standard file type
set ffs=unix,dos,mac

" Turn backup off, since most stuff is in svn / git anyway...
set nobackup
set nowb
set noswapfile

" Enable filetype plugins
filetype plugin indent on

" Start Pathogen plugin to load bundle
call pathogen#infect()
call pathogen#helptags()

" ---------------------------------------------------------------- }}}

" Vim user interface --------------------------------------------- {{{

" Compatible options
set cpoptions+=$

" Title is name of current file
set title

" Set 5 lines to the cursor - when moving vertically using j / k
set scrolloff=5

" Turn on the WiLd menu
set wildmenu

" Behave shell-like when completing shell
set wildmode=longest,list

" Ignore compiled files and backups
set wildignore=*.o,*~,*.pyc,*.gch

" Show line numbers
set nonumber

" Always show current position
set ruler

" Height of the command bar
set cmdheight=2

" Configure backspace so it acts as it should act
set backspace=eol,start,indent
set whichwrap+=<,>,h,l

" No annoying sound on errors
set noerrorbells
set novisualbell
set t_vb=
set timeoutlen=500

" Hide the mouse pointer while typing
set mousehide

" Deactivate mouse entirely
set mouse=

" Don't redraw while executing macros (good performance config)
set lazyredraw

" ---------------------------------------------------------------- }}}

" Syntax related ------------------------------------------------- {{{

" Enable syntax highlighting
syntax enable

" Show matching brackets when text indicator is over them
set showmatch

" Fold settings
set foldmethod=indent

" Max number of fold levels
set foldnestmax=10

" Folding everything
set nofoldenable

" Again, folding parent levels
set foldlevel=1

" How many tenths of a second to blink when matching brackets
set matchtime=2

" For regular expressions turn magic on
set magic

" Default completing function
set omnifunc=syntaxcomplete#Complete

" ---------------------------------------------------------------- }}}

" Status line ---------------------------------------------------- {{{

" Always put a status line in, even if there is only one window
set laststatus=2

" Status line stolen from Derek Wyatt's configuration:
"  - found here : "https://github.com/derekwyatt/vim-config/blob/master/vimrc"
set statusline=%f\ %m\ %r%{fugitive#statusline()}\ Line:%l/%L[%p%%]\ Col:%v\ Buf:#%n\ [%b][0x%B]

" Status line stolen from Top Vimbits
"  - found here : "http://vimbits.com/bits/95"
"set statusline=%F%m%r%h%w\ [FORMAT=%{&ff}]\ [TYPE=%Y]\ [ASCII=\%03.3b]\ [HEX=\%02.2B]\ [POS=%04l,%04v][%p%%]\ [LEN=%L]

" ---------------------------------------------------------------- }}}

" Text, tab and indent related ----------------------------------- {{{

" Use spaces instead of tabs
set expandtab

" Be smart when using tabs
set smarttab

" Indent by 4 spaces, 8 for tabs
set shiftwidth=4
set softtabstop=4
set tabstop=8

" Invisible characters
set nolist
set listchars=tab:▸\ ,eol:¬,trail:⋅,extends:❯,precedes:❮
set showbreak=↪

" Virtual edit (awesome)
set virtualedit=all

" Explicit FTW
set autoindent
set smartindent
set wrap

" ---------------------------------------------------------------- }}}

" Searching ------------------------------------------------------ {{{

" Ignore case
set ignorecase

" Try to be smart about cases
set smartcase

" Highlight results
set hlsearch

" Makes search act like in modern browsers
set incsearch

" ---------------------------------------------------------------- }}}

" Colors and Fonts ----------------------------------------------- {{{

" Dark backgrounds are better
set background=dark

" Colorschemes:
"  1 - jellybeans
"  2 - obsidian
"  3 - mustang
"  4 - wombat
"  5 - tesla
"  6 - monokai
"  7 - inkpot
colorscheme obsidian

" Editing font
set guifont=Droid\ Sans\ Mono\ 11

" Set extra options when running in GUI mode
if has("gui_running")
    set go-=r
    set go-=R
    set go-=l
    set go-=L
    set go-=m
    set go-=T
    set go+=e
    set t_Co=256
    set gtl=%M\ %t
endif

" Configure for terminal
if &term =~ "xterm"
    if has("terminfo")
        let g:CommandTCancelMap=['<ESC>','<C-c>']
        set t_Co=256
        set t_Sf=<Esc>[3%p1%dm
        set t_Sb=<Esc>[4%p1%dm
    else
        set t_Co=8
        set t_Sf=<Esc>[3%dm
        set t_Sb=<Esc>[4%dm
    endif
endif

" ---------------------------------------------------------------- }}}

" Moving around, tabs, windows and buffers ----------------------- {{{

" A buffer becomes hidden when it is abandoned
set hidden

" Specify the behavior when switching between buffers
set switchbuf=useopen,usetab,newtab

" Tab display
set showtabline=0

" Split towards the right
set splitright

if has("autocmd")
    " Return to last edit position when opening files
    autocmd BufReadPost *
                \ if line("'\"") > 0 && line("'\"") <= line("$") |
                \   exe "normal! g`\"" |
                \ endif

    " Cd directory when opening files
    autocmd BufEnter * execute "chdir ".escape(expand("%:p:h"), ' ')
endif

" ---------------------------------------------------------------- }}}

" Various key remaps --------------------------------------------- {{{

" Treat long lines as break lines (useful when moving around in them)
nnoremap j gj
nnoremap k gk
nnoremap gj j
nnoremap gk k

" Useful mappings for managing tabs
nmap <silent> <leader>tn :tabnew<cr>
nmap <silent> <leader>to :tabonly<cr>
nmap <silent> <leader>tc :tabclose<cr>
nmap <silent> <C-tab> :tabnext<cr>
nmap <silent> <C-S-tab> :tabprevious<cr>

" Switch buffers quickly
nmap <silent> <leader>n :bn<cr>
nmap <silent> <leader>N :bp<cr>

" Open last (alternative) buffer
nmap <leader><leader> <C-^>

" Close buffer without closing window
nmap <silent> <leader>bd :Bclose!<cr>

" Close all buffers
nmap <silent> <leader>ba :1,1000bd!<cr>

" Move a line of text using ALT+[jk]
nnoremap <silent> <M-j> :m .+1<cr>==
nnoremap <silent> <M-k> :m .-2<cr>==
inoremap <silent> <M-j> <Esc>:m .+1<cr>==gi
inoremap <silent> <M-k> <Esc>:m .-2<cr>==gi
vnoremap <silent> <M-j> :m '>+1<cr>gv=gv
vnoremap <silent> <M-k> :m '<-2<cr>gv=gv

" Toggle invisibles display
nmap <silent> <leader>i :setlocal list!<cr>

" Toggle search highlighting
nmap <silent> <leader><cr> :nohl<cr>

" Center screen at cursor when searching
nnoremap n nzz
nnoremap } }zz

" Don't jump when searching with * / #
nnoremap * *<C-o>zz
nnoremap # #<C-o>zz

" Quick search-replace
nnoremap <leader>s :%s/\<<C-r><C-w>\>//ge<left><left><left>

" Select all text in current file
nmap <leader>a ggVG

" Remap Vim 0 to first non-blank character
nmap 0 ^

" Exchange "`" and "'"
nnoremap ' `
nnoremap ` '

" Map ";" to ":" (avoid typos)
noremap ; :

" Map Space to re-indenting (much faster)
nmap <space> =

" Resolve inconsistency between "Y" (line),
"   and "D" (til end of line)
"nmap D dd
nmap Y y$

" Visual block indenting: keep selection after indenting
vmap > >gv
vmap < <gv

" Disable arrow keys (forever...)
nnoremap <up> <nop>
nnoremap <down> <nop>
nnoremap <left> <nop>
nnoremap <right> <nop>

" Fast saving
nnoremap <silent> <leader>w :w<cr>

" Close current file
nmap <silent> <leader>q :q<cr>

" Edit / Source configuration
nmap <silent> <leader>co :e $MYVIMRC<cr>
nmap <silent> <leader>so :so $MYVIMRC<cr>

" Toggle paste mode on and off
nmap <silent> <leader>pp :setlocal paste!<cr>

" Toggle spell-checking
nmap <silent> <leader>ss :setlocal spell!<cr>

" Quick make command
map <silent> <leader>m :make<cr>

" ---------------------------------------------------------------- }}}

" Helper functions ----------------------------------------------- {{{

" Delete buffer while keeping window layout (don't close buffer's windows).
" Version 2008-11-18 from http://vim.wikia.com/wiki/VimTip165
if v:version < 700 || exists('loaded_bclose') || &cp
    finish
endif
let loaded_bclose = 1
if !exists('bclose_multiple')
    let bclose_multiple = 1
endif

" Display an error message.
function! s:Warn(msg) " --- {{{
    echohl ErrorMsg
    echomsg a:msg
    echohl NONE
endfunction " ------------- }}}

" Command ':Bclose' executes ':bd' to delete buffer in current window.
" The window will show the alternate buffer (Ctrl-^) if it exists,
" or the previous buffer (:bp), or a blank buffer if no previous.
" Command ':Bclose!' is the same, but executes ':bd!' (discard changes).
" An optional argument can specify which buffer to close (name or number).
function! s:Bclose(bang, buffer) " -------------- {{{
    if empty(a:buffer)
        let btarget = bufnr('%')
    elseif a:buffer =~ '^\d\+$'
        let btarget = bufnr(str2nr(a:buffer))
    else
        let btarget = bufnr(a:buffer)
    endif
    if btarget < 0
        call s:Warn('No matching buffer for '.a:buffer)
        return
    endif
    if empty(a:bang) && getbufvar(btarget, '&modified')
        call s:Warn('No write since last change for buffer '.btarget.' (use :Bclose!)')
        return
    endif
    " Numbers of windows that view target buffer which we will delete.
    let wnums = filter(range(1, winnr('$')), 'winbufnr(v:val) == btarget')
    if !g:bclose_multiple && len(wnums) > 1
        call s:Warn('Buffer is in multiple windows (use ":let bclose_multiple=1")')
        return
    endif
    let wcurrent = winnr()
    for w in wnums
        execute w.'wincmd w'
        let prevbuf = bufnr('#')
        if prevbuf > 0 && buflisted(prevbuf) && prevbuf != w
            buffer #
        else
            bprevious
        endif
        if btarget == bufnr('%')
            " Numbers of listed buffers which are not the target to be deleted.
            let blisted = filter(range(1, bufnr('$')), 'buflisted(v:val) && v:val != btarget')
            " Listed, not target, and not displayed.
            let bhidden = filter(copy(blisted), 'bufwinnr(v:val) < 0')
            " Take the first buffer, if any (could be more intelligent).
            let bjump = (bhidden + blisted + [-1])[0]
            if bjump > 0
                execute 'buffer '.bjump
            else
                execute 'enew'.a:bang
            endif
        endif
    endfor
    execute 'bdelete'.a:bang.' '.btarget
    execute wcurrent.'wincmd w'
endfunction " ----------------------------------- }}}
command! -bang -complete=buffer -nargs=? Bclose call s:Bclose('<bang>', '<args>')

" ---- }}}

" FileType settings ---------------------------------------------- {{{

if has("autocmd")
    " Vimrc
    autocmd BufNewFile,BufRead,BufEnter *vimrc set foldmethod=marker

    " Makefile
    autocmd FileType make setl noexpandtab

    " C / C++
    autocmd BufNewFile,BufRead,BufEnter *.hpp setl filetype=cpp

    " JSON / JavaScript
    autocmd BufNewFile,BufRead *.json set filetype=javascript

    " Pascal
    autocmd BufNewFile,BufRead *.pas,*.PAS set filetype=pascal

    " Markdown
    autocmd BufNewFile,BufRead *.md set filetype=markdown

    " Python
    autocmd BufWritePre *.py :%s/\s\+$//e
endif

" ---------------------------------------------------------------- }}}

" Plugin customization ------------------------------------------- {{{

" Fugitive
nmap <silent> <leader>gs :Gstatus<cr>
nmap <silent> <leader>gd :Gdiff<cr>
nmap <silent> <leader>gl :Glog<cr>
nmap <silent> <leader>gc :Gcommit<cr>
nmap <silent> <leader>gw :Gwrite<cr>

" Gist
let g:gist_detect_filetype = 1
let g:gist_show_privates = 1

" ---------------------------------------------------------------- }}}
